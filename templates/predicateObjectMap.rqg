PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rr: <http://www.w3.org/ns/r2rml#>
PREFIX rml: <http://semweb.mmlab.be/ns/rml#>
PREFIX ql: <http://semweb.mmlab.be/ns/ql#>
PREFIX ex: <http://example.com/ns#>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX st: <http://ns.inria.fr/sparql-template/>

BASE <http://w3id.org/SPARQL-Generate-RML/>

# https://www.w3.org/TR/r2rml/ section 7.0
TEMPLATE <templates/predicateObjectMap.rqg>( ?mapping, ?predicateObjectMap, ?referenceFormulation) {

  TEMPLATE { st:turtle(?predicate) " " st:turtle(?object) } 
  WHERE { FILTER( BOUND(?predicate) && BOUND(?object) ) }.

  TEMPLATE { st:turtle(?predicate) " "  TEMPLATE <templates/termMap.rqg>(?mapping, ?objectMap, ?referenceFormulation, rr:ObjectMap) . } 
  WHERE { FILTER( BOUND(?predicate) && BOUND(?objectMap) ) }.

  TEMPLATE { TEMPLATE <templates/termMap.rqg>(?mapping, ?predicateMap, ?referenceFormulation, rr:PredicateMap). " " st:turtle(?object) } 
  WHERE { FILTER( BOUND(?predicateMap) && BOUND(?object) ) }.

  TEMPLATE { TEMPLATE <templates/termMap.rqg>(?mapping, ?predicateMap, ?referenceFormulation, rr:PredicateMap). " " TEMPLATE <templates/termMap.rqg>(?mapping, ?objectMap, ?referenceFormulation, rr:ObjectMap) . } 
  WHERE { FILTER( BOUND(?predicateMap) && BOUND(?objectMap) ) }.
  
} WHERE {
  { ?predicateObjectMap rr:predicateMap ?predicateMap } UNION { ?predicateObjectMap rr:predicate ?predicate }
  { ?predicateObjectMap rr:objectMap  ?objectMap } UNION { ?predicateObjectMap rr:object ?object }
}